/* derived from
https://github.com/exactpro/sailfish-core/blob/f47edf4b/BackEnd/Core/sailfish-common/build.gradle

  Copyright 2009-2019 Exactpro (Exactpro Systems Limited)

  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

      http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
*/

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.32'
}

repositories {
   mavenCentral()
}

ext {
  srcDir     = file('src')
  mainDir    = file("${srcDir}/main")
  mainResDir = file("${mainDir}/resources")
  genDir     = file("${srcDir}/gen")
  genJavaDir = file("${genDir}/java")
  genResDir  = file("${genDir}/resources")
  templatesDir   = "${mainDir}/templates"
  xsdDir      = "${mainResDir}/xsd"
  providedDir = file("${srcDir}/provided")
  providedResDir = file("${providedDir}/resources/")
}

sourceSets {
  main {
    java {
      srcDir genJavaDir
    }
    resources {
      srcDir genResDir
      srcDir providedResDir
    }
  }
}

configurations {
    jamon
    jaxb
}

dependencies {
    jamon 'org.jamon:jamon-anttask:2.4.2'

    jaxb 'com.sun.xml.bind:jaxb-xjc:2.1.3'
    jaxb 'javax.xml:jsr173:1.0'
    jaxb 'javax.activation:activation:1.1.1'

    implementation 'com.exactpro.mina:apache-mina-core:2.0.21.2'

    implementation 'org.jamon:jamon-runtime:2.4.1'
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.2"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.2"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.2"
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'net.sourceforge.javacsv:javacsv:2.0'

    // For services/codecs/CodecMessageFilter.java (from sailfish-core)
    implementation 'com.google.guava:guava:30.1-jre'
    implementation("org.apache.commons:commons-collections4:4.4")

    compileOnly "org.apache.logging.log4j:log4j-core:2.19.0"
    compileOnly "com.google.code.findbugs:jsr305:3.0.2" // import javax.annotation.concurrent.ThreadSafe

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.guava:guava:30.1-jre'
    testImplementation "com.fasterxml.jackson.core:jackson-databind:2.14.1"
    testImplementation 'com.thoughtworks.xstream:xstream:1.4.20'
}

tasks.register('templates') {
    doLast {
      ant.taskdef(name: 'jamon', classname: 'org.jamon.ant.JamonTask',   classpath: configurations.jamon.asPath)
      ant.taskdef(name: 'xjc',   classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.jaxb.asPath)
      ant.jamon(destdir: "${genJavaDir}", srcdir: "${templatesDir}")
      ant.xjc(schema: "${xsdDir}/dictionary.xsd",
              destdir: "${genJavaDir}",
              package: "com.exactpro.sf.common.impl.messages.xml.configuration", removeOldOutput: "yes"
      ) {
        produces(dir: "${genJavaDir}/com/exactpro/common/sf/impl/messages/xml/configuration", includes: "*")
        arg(value: "-extension")
      }
    }
}

compileKotlin.dependsOn.add("templates")
